import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
}

group 'selab.csie.ntu'
version '1.0'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

dependencies {
    implementation 'javax.servlet:javax.servlet-api:4.0.1'
    implementation 'com.google.guava:guava:29.0-jre'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task generateJavaConfig(type: Copy) {
    from 'src/main/resources/template/Config.java'
    into "${buildDir}/generated/java"
    filter(ReplaceTokens, tokens: [
            CLIENT_HOST_VAL: project.hasProperty('acfix.client.host') ?
                    project.getProperties().get('acfix.client.host') : 'localhost',
            CLIENT_PORT_VAL: project.hasProperty('acfix.client.port') ?
                    project.getProperties().get('acfix.client.port') : '4200'
    ])
}

sourceSets.main.java.srcDir "${buildDir}/generated/java"
compileJava.dependsOn generateJavaConfig
